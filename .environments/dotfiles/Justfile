set working-directory := "../.."

# View help text
@help *args:
    .environments/dotfiles/scripts/help.nu {{ args }}

# Check configuration flake
[working-directory("./configuration")]
@check *args:
    ../.environments/dotfiles/scripts/check.nu {{ args }}

# Run `prune` and `optimise`
@clean *args:
    .environments/dotfiles/scripts/clean.nu {{ args }}

# List available configurations
@configurations *args:
    .environments/dotfiles/scripts/configurations.nu {{ args }}

alias configs := configurations

# View the diff between hosts
[no-cd]
@diff *args:
    .environments/dotfiles/scripts/diff.nu {{ args }}

# List configuration files
[no-cd]
@files *args:
    .environments/dotfiles/scripts/files.nu {{ args }}

# View generations
@generations *help:
    .environments/dotfiles/scripts/generations.nu {{ help }}

# List flake inputs
[working-directory("./configuration")]
@inputs *help:
    ../.environments/dotfiles/scripts/inputs.nu {{ help }}

# Build ISO
@iso *args:
    .environments/dotfiles/scripts/iso.nu {{ args }}

# Replace identical files in the Nix store by hard links
@optimise *help:
    .environments/dotfiles/scripts/optimise.nu {{ help }}

# Collect garbage and remove old generations
@prune *args:
    .environments/dotfiles/scripts/prune.nu {{ args }}

# Rebuild and switch to (or --test) a configuration
[working-directory("./configuration")]
@rebuild *args:
    ../.environments/dotfiles/scripts/rebuild.nu {{ args }}

# Rollback to a previous generation
[macos]
@rollback *id:
    .environments/dotfiles/scripts/rollback.nu {{ id }}

# Open Nix REPL with flake loaded
@shell *help:
    .environments/dotfiles/scripts/shell.nu {{ help }}

# Update dependencies
[working-directory("./configuration")]
@update *inputs:
    ../.environments/dotfiles/scripts/update.nu {{ inputs }}
